# [description]
# Verify that gl_ClipDistance works properly when used as a function
# "in" parameter.
#
# This test sets up gl_ClipDistance to do some clipping.  Then it
# passes gl_ClipDistance as an in parameter to a function.
#
# Then it checks two things:
# - That no data was corrupted when passing the array into the function
# - That clipping occurred as expected
#
# To check that clipping occurred as expected, the 8 gl_ClipDistance
# values are used to clip a rectangle to an octagon shape.
#
# The octagon is centered at (0.5, 0.5), and has a small radius
# (distance from center to perpendicular edge) of 0.4.

[require]
GLSL >= 1.30

[vertex shader]
#version 130

out float gl_ClipDistance[8];

vec4 foo(in float clip_distance_copy[8])
{
	for (int i = 0; i < 8; ++i) {
		if (clip_distance_copy[i] != gl_ClipDistance[i]) {
			return vec4(1.0, 0.0, 0.0, 1.0);
		}
	}
	return vec4(0.0, 1.0, 0.0, 1.0);
}

void main(void)
{
	gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;

	// Compute 2D coordinates relative to a center point of (0.5,
	// 0.5).
	vec2 coord = gl_Vertex.xy - vec2(0.5, 0.5);

	for (int i = 0; i < 8; ++i) {
		// Compute a unit vector in the direction i*45deg from
		// the x axis.
		float angle = i*(3.141592653589793/4);
		vec2 u = vec2(cos(angle), sin(angle));

		// Reject points whose 2D coordinate, projected onto
		// that unit vector, is greater than 0.4.
		gl_ClipDistance[i] = 0.4 - dot(u, coord);
	}

	gl_FrontColor = foo(gl_ClipDistance);
}

[fragment shader]
#version 130
void main(void)
{
	gl_FragColor = gl_Color;
}

[test]
clear color 0.0 0.0 0.0 0.0
clear
ortho 0 1 0 1
enable GL_CLIP_PLANE0
enable GL_CLIP_PLANE1
enable GL_CLIP_PLANE2
enable GL_CLIP_PLANE3
enable GL_CLIP_PLANE4
enable GL_CLIP_PLANE5
enable GL_CLIP_PLANE6
enable GL_CLIP_PLANE7
draw rect 0.0 0.0 1.0 1.0

# Test points inside each octagon edge
relative probe rgba (0.850, 0.500) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.747, 0.747) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.500, 0.850) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.253, 0.747) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.150, 0.500) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.253, 0.253) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.500, 0.150) (0.0, 1.0, 0.0, 1.0)
relative probe rgba (0.747, 0.253) (0.0, 1.0, 0.0, 1.0)

# Test points outside each octagon edge
relative probe rgba (0.950, 0.500) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.818, 0.818) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.500, 0.950) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.182, 0.818) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.050, 0.500) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.182, 0.182) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.500, 0.050) (0.0, 0.0, 0.0, 0.0)
relative probe rgba (0.818, 0.182) (0.0, 0.0, 0.0, 0.0)
